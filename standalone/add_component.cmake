cmake_minimum_required (VERSION 3.24 FATAL_ERROR)

include(${CMAKE_CURRENT_LIST_DIR}/../utils/missing_error.cmake)

macro(add_component PACKAGE)
  cmake_parse_arguments(AC "" "TARGET;DIR" "" ${ARGN})

  if (${AC_UNPARSED_ARGUMENTS})
    message(FATAL_ERROR "Too many arguments to add_component")
  endif()
  
  if (DEFINED AC_TARGET)
    set(TARGET ${AC_TARGET})
  else()
    set(TARGET ${PACKAGE}::${PACKAGE})
  endif()
  
  if (DEFINED AC_DIR)
    set(DIR ${AC_DIR})
  else()
    set(DIR ${PACKAGE})
  endif()

  if (NOT TARGET ${TARGET})
    set(SOURCE_DIR ${CPP_CORE_SOURCE_COMPONENT_DIR}/${DIR})
    set(BINARY_DIR ${CPP_CORE_BINARY_COMPONENT_DIR}/${DIR})
    if (EXISTS ${SOURCE_DIR})
      message("-- add_component ${PACKAGE} subdir(${SOURCE_DIR})")
      add_subdirectory(${SOURCE_DIR} ${BINARY_DIR})
    else()
      message("-- add_component ${PACKAGE} find_package")
      find_package(${PACKAGE} QUIET)
      if (NOT TARGET ${TARGET})
	missing_error(${PACKAGE} ${TARGET} ${SOURCE_DIR})
      endif()
    endif()
  endif()
endmacro()
